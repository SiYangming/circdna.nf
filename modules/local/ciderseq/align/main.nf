process CIDERSEQ_ALIGN {
    tag "$meta.id"
    label 'process_low'

    conda "${moduleDir}/environment.yml"
    container "${ workflow.containerEngine in ['singularity', 'apptainer'] && !task.ext.singularity_pull_docker_container ?
        'docker://quay.io/bioinfortools/ciderseq2:2.0.0' :
        'quay.io/bioinfortools/ciderseq2:2.0.0' }"

    input:
    tuple val(meta), path(reads)
    path(config)
    path(targets)
    val(genome)

    output:
    tuple val(meta), path("${prefix}.fa"), emit: aligned
    path "versions.yml"                   , emit: versions

    when:
    task.ext.when == null || task.ext.when

    script:
    def args = task.ext.args ?: ''
    prefix = task.ext.prefix ?: "${meta.id}.${genome}"
    """
    python ciderseq_align.py \\
        --config ${config} \\
        --targets ${targets} \\
        --genome ${genome} \\
        --outdir . \\
        --prefix ${prefix} \\
        $args \\
        ${reads}

    cat <<-END_VERSIONS > versions.yml
    "${task.process}":
        ciderseq2: 2.0.0
    END_VERSIONS
    """

    stub:
    prefix = task.ext.prefix ?: "${meta.id}.${genome}"
    """
    touch ${prefix}.fa

    cat <<-END_VERSIONS > versions.yml
    "${task.process}":
        ciderseq2: 2.0.0
    END_VERSIONS
    """
}
